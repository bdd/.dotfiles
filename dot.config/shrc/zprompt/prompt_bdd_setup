# vim: filetype=zsh

prompt_bdd_setup() {
  local \
    co_fade=245 \
    co_error=203 \
    co_attention=224 \
    co_highlight=230

  colorize_by_os() {
    local color
    case "${OSTYPE}" in
      darwin*) color="red"    ;;
      linux*)  color="yellow" ;;
      *)       color="cyan"   ;;
    esac

    echo "%F{${color}}$*%f"
  }

  PS1="%~"
  PS1+="$(colorize_by_os '%#')"  # cwd relative to home and '%' (or '#' if root)
  # Notice ternary expression: %(<n>check.true-text.false-text).
  PS1+="%F{${co_highlight}}%(2L.%%.)%f"  # '%' if shell level is >= 2.
  PS1+="%F{${co_attention}}%(1j.&.)%f"  # '&' if there are background jobs.
  PS1+=" "  # separate prompt from command input

  RPROMPT="%F{${co_error}}%(?..%?)%f"  # exit status of last process is not 0
  if [[ -z ${TMUX} ]]; then
    RPROMPT+=" %F{${co_fade}}%m%f"
  fi

  if (($# > 0)); then
    PS1="$1:${PS1}"
  fi
}

prompt_bdd_setup "$@"
