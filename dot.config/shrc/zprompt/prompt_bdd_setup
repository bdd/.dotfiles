# vim: filetype=zsh

prompt_bdd_setup() {
  colorize_by_os() {
    local color
    case "${OSTYPE}" in
      darwin*) color="red"    ;;
      linux*)  color="green"  ;;
      *)       color="yellow" ;;
    esac

    echo "%F{${color}}$*%f"
  }

  PS1="%~$(colorize_by_os '%#')"  # cwd relative to home and '%' (or '#' if root)
  # Notice ternary expression: %(<n>check.true-text.false-text).
  PS1+="%F{blue}%(1j.&.)%f"  # '&' if there are background jobs.
  PS1+=" "  # separate prompt from command input

  RPROMPT="%F{red}%(?..%?)%f"  # exit status of last process is not 0

  if (($# > 0)); then
    case $1 in
      norprompt) unset RPROMPT; shift ;;
      rprompt-time) RPROMPT+=" %*"; shift ;;
      rprompt-hom) RPROMPT+=" %M ${OSTYPE} ${MACHTYPE}"; shift ;;
    esac

    if (($# > 0)); then
      PS1="$1:${PS1}"
    fi
  fi
}

prompt_bdd_setup "$@"
